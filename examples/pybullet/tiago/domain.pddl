(define (domain tiago-tamp)
  (:requirements :strips :equality)
  (:constants @sink @stove)
  (:predicates
    (Arm ?a)
    (Stackable ?o ?r)
    (Sink ?r)
    (Stove ?r)
    (Type ?t ?b)

    (Pose ?o ?p)
    (Grasp ?o ?g)
    (Alignment ?o ?p1 ?p2 ?g)
    (Kin ?a ?o ?p ?g ?q ?t)
    (Kin2 ?a ?o ?p1 ?p2 ?g ?q1 ?q2 ?t)
    (BaseMotion ?q1 ?t ?q2)
    (ArmMotion ?a ?p1 ?p2 ?q1 ?q2 ?t)
    (Supported ?o ?p ?r)
    (BTraj ?t)
    (ATraj ?t)

    (CFreePosePose ?o ?p ?o2 ?p2)
    (CFreeApproachPose ?o ?p ?g ?o2 ?p2)
    (CFreeAlignPose ?o ?p1 ?p3 ?g ?o2 ?p2)
    (CFreeTrajPose ?t ?o2 ?p2)
    (CFreeTrajGraspPose ?t ?a ?o1 ?g1 ?o2 ?p2)

    (AtPose ?o ?p)
    (AtGrasp ?a ?o ?g)
    (AtPush ?a ?o ?p ?g ?q)
    (HandEmpty ?a)
    (AtBConf ?q)
    (AtAConf ?a ?q)
    (CanMove)
    (Cleaned ?o)
    (Cooked ?o)

    (On ?o ?r)
    (Holding ?a ?o)
    (UnsafePose ?o ?p)
    (UnsafeApproach ?o ?p ?g)
    (UnsafeAlignment ?o ?p1 ?p2 ?g)
    (UnsafeATraj ?t)
    (UnsafeBTraj ?t)
  )
  (:functions
    (Distance ?q1 ?q2)
    (MoveCost ?t)
    (PickCost)
    (PlaceCost)
    (AlignCost)
    (PushCost)
  )

  (:action move_base
    :parameters (?q1 ?q2 ?t)
    :precondition (and (BaseMotion ?q1 ?t ?q2)
                       (AtBConf ?q1) (CanMove)
                       ;(not (UnsafeBTraj ?t))
                   )
    :effect (and (AtBConf ?q2)
                 (not (AtBConf ?q1)) (not (CanMove))
                 (increase (total-cost) (Distance ?q1 ?q2)))
                 ; (increase (total-cost) (MoveCost ?t)))
  )
  ; (:action move_arm
  ;  :parameters (?a ?q0 ?q1 ?q2 ?t)
  ;  :precondition (and (ArmMotion ?a ?q0 ?q1 ?q2 ?t)
  ;                     (AtAConf ?a ?q1)
  ;                     (AtBConf ?q0)
  ;                )
  ;  :effect (and (AtAConf ?a ?q2)
  ;               (not (AtAConf ?a ?q1))
  ;               (increase (total-cost) (AlignCost)))
  ; )

  (:action pick
    :parameters (?a ?o ?p ?g ?q ?t)
    :precondition (and (Kin ?a ?o ?p ?g ?q ?t)
                       (AtPose ?o ?p) (HandEmpty ?a) (AtBConf ?q)
                       (not (UnsafeApproach ?o ?p ?g))
                       (not (UnsafeATraj ?t))
                  )
    :effect (and (AtGrasp ?a ?o ?g) (CanMove)
                 (not (AtPose ?o ?p)) (not (HandEmpty ?a))
                 (increase (total-cost) (PickCost)))
  )
  (:action place
    :parameters (?a ?o ?p ?g ?q ?t)
    :precondition (and (Kin ?a ?o ?p ?g ?q ?t)
                       (AtGrasp ?a ?o ?g) (AtBConf ?q)
                       (not (UnsafePose ?o ?p))
                       (not (UnsafeApproach ?o ?p ?g))
                       (not (UnsafeATraj ?t))
                  )
    :effect (and (AtPose ?o ?p) (HandEmpty ?a) (CanMove)
                 (not (AtGrasp ?a ?o ?g))
                 (increase (total-cost) (PlaceCost)))
  )
  (:action align
    :parameters (?a ?o ?p1 ?p2 ?g ?q0 ?q2 ?t)
    :precondition (and (Kin2 ?a ?o ?p1 ?p2 ?g ?q0 ?q2 ?t)
                       (AtPose ?o ?p1)
                       (HandEmpty ?a)
                       (AtBConf ?q0)
                       (not (UnsafePose ?o ?p2)) ;the goal pose must be collision-free
                       (not (UnsafeAlignment ?o ?p1 ?p2 ?g))
                       (not (UnsafeATraj ?t))
                  )
    :effect (and (not (CanMove)) (AtPush ?a ?o ?p2 ?g ?q2) (AtAConf ?a ?q2)
                 (increase (total-cost) (AlignCost)))
  )
  (:action push
    :parameters (?a ?o ?p1 ?p2 ?g ?q0 ?q1 ?t)
    :precondition (and (ArmMotion ?a ?p1 ?p2 ?q0 ?q1 ?t)
                       (AtPush ?a ?o ?p2 ?g ?q1)
                       (AtAConf ?a ?q1)
                       (AtBConf ?q0)
                       (not (UnsafeATraj ?t))
                       (HandEmpty ?a)
                       (AtPose ?o ?p1)
                  )
    :effect (and (AtPose ?o ?p2) (CanMove)
                 (not (AtPush ?a ?o ?p2 ?g ?q1))
                 (not (AtAConf ?a ?q1))
                 (increase (total-cost) (PushCost)))
  )

  (:action clean
    :parameters (?o ?r)
    :precondition (and (Stackable ?o ?r) (Type ?r @sink) ; (Sink ?r)
                       (On ?o ?r))
    :effect (Cleaned ?o)
  )
  (:action cook
    :parameters (?o ?r)
    :precondition (and (Stackable ?o ?r) (Type ?r @stove) ; (Stove ?r)
                       (On ?o ?r) (Cleaned ?o))
    :effect (and (Cooked ?o)
                 (not (Cleaned ?o)))
  )

  (:derived (On ?o ?r)
    (exists (?p) (and (Supported ?o ?p ?r)
                      (AtPose ?o ?p)))
  )
  (:derived (Holding ?a ?o)
    (exists (?g) (and (Arm ?a) (Grasp ?o ?g)
                      (AtGrasp ?a ?o ?g)))
  )

  (:derived (UnsafePose ?o ?p)
    (exists (?o2 ?p2) (and (Pose ?o ?p) (Pose ?o2 ?p2) (not (= ?o ?o2))
                           (not (CFreePosePose ?o ?p ?o2 ?p2))
                           (AtPose ?o2 ?p2)))
  )
  (:derived (UnsafeApproach ?o ?p ?g)
    (exists (?o2 ?p2) (and (Pose ?o ?p) (Grasp ?o ?g) (Pose ?o2 ?p2) (not (= ?o ?o2))
                           (not (CFreeApproachPose ?o ?p ?g ?o2 ?p2))
                           (AtPose ?o2 ?p2)))
  )
  (:derived (UnsafeAlignment ?o ?p0 ?p1 ?g)
    (exists (?o2 ?p2) (and (Pose ?o ?p0) (Alignment ?o ?p0 ?p1 ?g) (Pose ?o2 ?p2) (not (= ?o ?o2))
                           (not (CFreeAlignPose ?o ?p0 ?p1 ?g ?o2 ?p2))
                           (AtPose ?o2 ?p2)))
  )
  (:derived (UnsafeATraj ?t)
    (exists (?o2 ?p2) (and (ATraj ?t) (Pose ?o2 ?p2)
                           (not (CFreeTrajPose ?t ?o2 ?p2))
                           (AtPose ?o2 ?p2)))
  )
  ;(:derived (UnsafeBTraj ?t) (or
  ;  ;(exists (?o2 ?p2) (and (BTraj ?t) (Pose ?o2 ?p2)
  ;  ;                       (not (CFreeTrajPose ?t ?o2 ?p2))
  ;  ;                       (AtPose ?o2 ?p2)))
  ;  (exists (?a ?o1 ?g1 ?o2 ?p2) (and (BTraj ?t) (Arm ?a) (Grasp ?o1 ?g1) (Pose ?o2 ?p2)
  ;                                    (not (CFreeTrajGraspPose ?t ?a ?o1 ?g1 ?o2 ?p2)) (not (= ?o1 ?o2))
  ;                                    (AtGrasp ?a ?o1 ?g1) (AtPose ?o2 ?p2)))
  ;))

)